# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("//foundation/appexecfwk/standard/appexecfwk.gni")
import(
    "//foundation/appexecfwk/standard/services/bundlemgr/appexecfwk_bundlemgr.gni")

module_output_path = "bundle_framework/bundlemgrservice"

ohos_unittest("BmsBundleKitServiceTest") {
  use_exceptions = true
  module_out_path = module_output_path
  include_dirs = [
    "//third_party/jsoncpp/include",
    "//base/startup/syspara_lite/interfaces/innerkits/native/syspara/include",
  ]
  sources = [
    "${services_path}/bundlemgr/src/account_helper.cpp",
    "${services_path}/bundlemgr/src/bundle_clone_mgr.cpp",
    "${services_path}/bundlemgr/src/bundle_data_mgr.cpp",
    "${services_path}/bundlemgr/src/bundle_data_storage_database.cpp",
    "${services_path}/bundlemgr/src/bundle_mgr_host_impl.cpp",
    "${services_path}/bundlemgr/src/bundle_mgr_service.cpp",
    "${services_path}/bundlemgr/src/bundle_mgr_service_event_handler.cpp",
    "${services_path}/bundlemgr/src/bundle_scanner.cpp",
    "${services_path}/bundlemgr/src/bundle_status_callback_death_recipient.cpp",
    "${services_path}/bundlemgr/src/bundle_user_mgr_host_impl.cpp",
    "${services_path}/bundlemgr/src/distributed_data_storage.cpp",
    "${services_path}/bundlemgr/src/hidump_helper.cpp",
    "${services_path}/bundlemgr/src/installd/installd_host_impl.cpp",
    "${services_path}/bundlemgr/src/installd/installd_operator.cpp",
    "${services_path}/bundlemgr/src/installd/installd_service.cpp",
    "${services_path}/bundlemgr/src/kvstore_death_recipient_callback.cpp",
    "${services_path}/bundlemgr/src/permission_changed_death_recipient.cpp",
    "${services_path}/bundlemgr/src/preinstall_data_storage.cpp",
  ]

  sources += [
    "${services_path}/bundlemgr/test/mock/src/accesstoken_kit.cpp",
    "${services_path}/bundlemgr/test/mock/src/mock_bundle_status.cpp",
    "${services_path}/bundlemgr/test/mock/src/mock_clean_cache.cpp",
    "${services_path}/bundlemgr/test/mock/src/system_ability_helper.cpp",
  ]

  sources += bundle_install_sources

  sources += [ "bms_bundle_kit_service_test.cpp" ]

  configs = [
    "${services_path}/bundlemgr/test:bundlemgr_test_config",
    "${libs_path}/libeventhandler:libeventhandler_config",
    "${innerkits_path}/appexecfwk_base:appexecfwk_base_sdk_config",
  ]
  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  deps = [
    "${libs_path}/libeventhandler:libeventhandler_target",
    "//base/security/appverify/interfaces/innerkits/appverify:libhapverify",
    "//base/security/permission/interfaces/innerkits/permission_standard/permissionsdk:libpermissionsdk_standard",
    "//base/startup/syspara_lite/interfaces/innerkits/native/syspara:syspara",
    "//foundation/aafwk/standard/interfaces/innerkits/ability_manager:ability_manager",
    "//foundation/aafwk/standard/interfaces/innerkits/base:base",
    "//foundation/aafwk/standard/interfaces/innerkits/want:want",
    "//foundation/distributeddatamgr/distributeddatamgr/interfaces/innerkits/distributeddata:distributeddata_inner",
    "//foundation/distributedschedule/safwk/interfaces/innerkits/safwk:system_ability_fwk",
    "//foundation/distributedschedule/samgr/interfaces/innerkits/samgr_proxy:samgr_proxy",
    "//third_party/googletest:gtest_main",
    "//third_party/jsoncpp:jsoncpp",
  ]

  if (bundle_framework_graphics) {
    include_dirs += [
      "//foundation/multimedia/image_standard/interfaces/innerkits/include",
    ]
    deps += [ "//foundation/multimedia/image_standard/interfaces/innerkits:image_native" ]
  }

  deps += bundle_install_deps

  external_deps = [
    "ability_runtime:app_manager",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "bytrace_standard:bytrace_core",
    "ces_standard:cesfwk_innerkits",
    "hiviewdfx_hilog_native:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
    "resmgr_standard:global_resmgr",
  ]

  defines = []
  if (configpolicy_enable) {
    external_deps += [ "config_policy:configpolicy_util" ]
    defines += [ "CONFIG_POLOCY_ENABLE" ]
  }
  if (build_selinux) {
    external_deps += [ "selinux:libhap_restorecon" ]
    cflags += [ "-DWITH_SELINUX" ]
  }
  if (account_enable) {
    external_deps += [ "os_account_standard:os_account_innerkits" ]
    defines += [ "ACCOUNT_ENABLE" ]
  }
  if (device_manager_enable) {
    sources += [ "${services_path}/bundlemgr/src/bms_device_manager.cpp" ]
    external_deps += [ "device_manager_base:devicemanagersdk" ]
    defines += [ "DEVICE_MANAGER_ENABLE" ]
  }
  if (hicollie_enable) {
    external_deps += [ "hicollie_native:libhicollie" ]
    defines += [ "HICOLLIE_ENABLE" ]
  }
  resource_config_file = "//foundation/appexecfwk/standard/test/resource/bundlemgrservice/ohos_test.xml"
}

group("unittest") {
  testonly = true

  deps = [ ":BmsBundleKitServiceTest" ]
}
